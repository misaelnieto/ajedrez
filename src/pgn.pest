WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
metadata_block = { "[" ~ word ~ "\"" ~ word ~ "\"]" }
word = { (!("]" | "\"") ~ ANY)+ }
files = { 'a'..'h' }
disambiguator = { "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" }
to_file = { 'a'..'h' }
to_rank = { '1' .. '8' }
move_comment = { "{" ~ (!("}") ~ ANY)* ~ "}" }
piece = { ("K" | "Q" | "R" | "B" | "N")? }
promoted_ranks = { ( "1" | "8" )}
promoted_piece = { ("Q" | "R" | "B" | "N")? }
castle_kingside = @{ "O-O" }
castle_queenside = @{ "O-O-O" }
move_piece_without_capture = ${ piece ~ to_file ~ to_rank }
move_piece_without_capture_disambiguator = ${ piece ~ disambiguator ~ to_file ~ to_rank }
move_piece_with_capture = ${ piece ~ disambiguator? ~ "x" ~ to_file ~ to_rank }
move_pawn_promotion = ${files ~ promoted_ranks ~ "=" ~ promoted_piece}
move_pawn_promotion_with_capture = ${files ~ "x" ~ files ~ promoted_ranks ~ "=" ~ promoted_piece}
move_piece = { (move_pawn_promotion | move_pawn_promotion_with_capture | move_piece_with_capture | move_piece_without_capture_disambiguator |move_piece_without_capture)}
complete_move = { (move_piece | castle_queenside | castle_kingside) ~ ("+" | "#")? }
move_number = @{ ASCII_DIGIT+}
move_pair = { move_number ~ "." ~ complete_move ~ move_comment? ~ ((ASCII_DIGIT)+ ~ "...")? ~ complete_move? ~ move_comment? }
move_list = { (move_pair)+ }
game_result = { "1/2-1/2" | "1-0" | "0-1" }
game = { SOI ~ (metadata_block)* ~ move_list ~ game_result ~ EOI}